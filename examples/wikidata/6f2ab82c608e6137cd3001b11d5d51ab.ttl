@prefix sh: <http://www.w3.org/ns/shacl#> .
@prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#> .
@prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#> .
@prefix dcterms: <http://purl.org/dc/terms/> .
@prefix schema: <https://schema.org/> .

<https://www.wikidata.org/#query-6f2ab82c608e6137cd3001b11d5d51ab> a sh:SPARQLExecutable,
    sh:SPARQLSelectExecutable;
  sh:select """#defaultView:BubbleChart
SELECT ?clabel ?count
WITH {
  SELECT DISTINCT ?item ?place ?placeLabel ?publishing_date ?gender ?language
  WHERE {
    ?item wdt:P291 ?place ;
      wdt:P577 ?publishing_date; 
      wdt:P31 ?inst. 
    FILTER(?inst IN (wd:Q1261026, wd:Q732577))
    FILTER(?place IN (wd:Q8692, wd:Q10313, wd:Q497801, wd:Q14318, wd:Q10282, wd:Q134674))
    FILTER(?publishing_date >= \"1700-01-01\"^^xsd:dateTime && ?publishing_date < \"1800-01-01\"^^xsd:dateTime)
    ?place rdfs:label ?placeLabel. FILTER(LANG(?placeLabel)=\"en\")
    OPTIONAL { ?item wdt:P872/wdt:P21/rdfs:label ?gender_ . FILTER(LANG(?gender_)=\"en\") }
    OPTIONAL { ?item wdt:P407/rdfs:label ?language_ . FILTER(LANG(?language_)=\"en\") }
    BIND(COALESCE(?gender_, \"unspecified\") AS ?gender)
    BIND(COALESCE(?language_, \"unspecified\") AS ?language)
  }
} AS %i
WHERE {
  {
    SELECT ?language (count(?item) as ?count)
    WHERE { INCLUDE %i }
    GROUP BY ?language
  }
  BIND(CONCAT(?language, \" \", STR(?count)) AS ?clabel)
}
ORDER BY DESC(?count)""";
  dcterms:isPartOf <https://www.wikidata.org//wiki/Wikidata:Request_a_query/Archive/2022/09>;
  dcterms:license <https://creativecommons.org/licenses/by-sa/4.0/>;
  sh:prefixes _:wikidata_prefixes;
  schema:target <https://query.wikidata.org/sparql/> .
