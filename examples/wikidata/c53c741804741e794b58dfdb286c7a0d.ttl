@prefix sh: <http://www.w3.org/ns/shacl#> .
@prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#> .
@prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#> .
@prefix dcterms: <http://purl.org/dc/terms/> .
@prefix schema: <https://schema.org/> .

<https://www.wikidata.org/#query-c53c741804741e794b58dfdb286c7a0d> a sh:SPARQLExecutable,
    sh:SPARQLSelectExecutable;
  sh:select """#defaultView:Map
SELECT DISTINCT ?item ?itemLabel ?pop ?coords ?rgb (?colour AS ?layer) 

WITH {
  SELECT ?colour ?rgb WHERE {
    VALUES ?sequence {wd:Q105849312} .     # desired colour progression
    VALUES ?n {3} .                        # desired no of points either side of centre -- in this case 3, to give numbers 1 to 7 

    ?sequence p:P527 ?stmt .
    ?stmt pq:P1545 ?seq .
    ?stmt pq:P465 ?rgb .

    ?sequence p:P2670/pq:P1114 ?len .
    BIND(((?len + 1.0)/2.0) AS ?centre) .
    BIND (floor((?centre - 1.0) / xsd:float(?n)) AS ?step) .
    BIND (?n + 1.0 + ((xsd:float(?seq) - ?centre)  / xsd:float(?step)) AS ?colour) .
  
    FILTER(?colour = floor(?colour))
    FILTER(?colour >= 1)
    FILTER(?colour <= 1 + 2* ?n)
  } ORDER BY ?colour
} AS %hex_colours


WHERE {  
  ?item wdt:P31/wdt:P279* wd:Q515 .
  ?item wdt:P17 wd:Q145.
  ?item wdt:P1082 ?pop .
  ?item wdt:P625 ?coords .
  BIND(xsd:float(floor(?pop / 100000)) AS ?colour) .
  INCLUDE %hex_colours .

  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }
} ORDER BY ?pop""";
  dcterms:isPartOf <https://www.wikidata.org//wiki/Talk:Q105849312>;
  dcterms:license <https://creativecommons.org/licenses/by-sa/4.0/>;
  sh:prefixes _:wikidata_prefixes;
  schema:target <https://query.wikidata.org/sparql/> .
