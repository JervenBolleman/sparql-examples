@prefix sh: <http://www.w3.org/ns/shacl#> .
@prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#> .
@prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#> .
@prefix dcterms: <http://purl.org/dc/terms/> .
@prefix schema: <https://schema.org/> .

<https://www.wikidata.org/#query-e41f775bbf5733e8eda0364fe9a3fc4d> a sh:SPARQLExecutable,
    sh:SPARQLSelectExecutable;
  sh:select """# Most cited authors

PREFIX target: <http://www.wikidata.org/entity/Q2779854>

SELECT
  ?count
  ?author ?authorLabel
  ?orcid
  ?example_cited_work ?example_cited_workLabel
  ?example_citing_work ?example_citing_workLabel
WITH {
  # Count the number of citations
  SELECT
    ?author
    (COUNT(?cited_work) AS ?count)
    (SAMPLE(?cited_work) AS ?example_cited_work)
    (SAMPLE(?citing_work) AS ?example_citing_work)
  WHERE {
    ?cited_work wdt:P1433 target: .
    ?citing_work wdt:P2860 ?cited_work .
    ?cited_work wdt:P50 ?author .
  }
  GROUP BY ?author
} AS %result
WHERE {
  INCLUDE %result 
          
  # Include optional ORCID iD
  OPTIONAL { ?author wdt:P496 ?orcid . }
  
  # Label the results
  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\" . } 
}
ORDER BY DESC(?count)
LIMIT 50""";
  dcterms:isPartOf <https://www.wikidata.org//wiki/User:Will_(Wiki_Ed)/APS_Project_Page/Queries>;
  dcterms:license <https://creativecommons.org/licenses/by-sa/4.0/>;
  sh:prefixes _:wikidata_prefixes, _:genid-9eb55b2583ef4ad09d9d48ff9f18ffec124454-wikidata_prefixes;
  schema:target <https://query.wikidata.org/sparql/> .
